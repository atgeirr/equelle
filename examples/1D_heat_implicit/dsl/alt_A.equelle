#! -*-sh-*-
# Heat conduction with no boundary conditions or source terms.

# Physics that requires specification
k : Scalar = UserSpecifiedScalarWithDefault(0.3) # Heat diffusion constant.

# @afr: time step strategy assumed given outside.
dt : Scalar = UserSpecifiedScalarWithDefault(0.5) # Time step length.

# u0 should be given initial values (in user input)
u0 : Collection Of Scalar On AllCells() = UserSpecifiedCollectionOfScalar( AllCells() )

# Trying to add support for Dirichlet boundaries
dirichlet_boundary : Collection Of Face On BoundaryFaces() = UserSpecifiedCollectionOfFace( BoundaryFaces() )
dirichlet_val : Collection Of Scalar On dirichlet_boundary = UserSpecifiedCollectionOfScalar( dirichlet_boundary )

# Compute interior transmissibilities.
vol = |AllCells()|                                         # Deduced type:  Collection Of Scalar On AllCells()
interior_faces = InteriorFaces()                           # Deduced type:  Collection Of Face
first = FirstCell(interior_faces)                          # Deduced type:  Collection Of Cell On interior_faces
							   # Equivalent to: Collection Of Cell On InteriorFaces()
second = SecondCell(interior_faces)                        # Deduced type:  Same as for 'first'.
itrans : Collection Of Scalar On interior_faces = k * |interior_faces| / |Centroid(first) - Centroid(second)| 

# Compute boundary transmissibilities.
bf = BoundaryFaces()
bf_cells = IsEmpty(FirstCell(bf)) ? SecondCell(bf) : FirstCell(bf)
bf_sign = IsEmpty(FirstCell(bf))) ? (-1 On bf_cells) : (1 On bf_cells)
btrans = k * |bf| / |Centroid(bf) - Centroid(bf_cells)|

# Compute flux for interior faces.
computeInteriorFlux : Function(u : Collection Of Scalar On AllCells()) -> Collection Of Scalar On InteriorFaces()
computeInteriorFlux(u) = {
    return -itrans * Gradient(u)
}

# Compute flux for boundary faces.
computeBoundaryFlux : Function(u : Collection Of Scalar On AllCells()) -> Collection Of Scalar On BoundaryFaces()
computeBoundaryFlux(u) = {
    # Compute flux at Dirichlet boundaries.
    dir_sign = bf_sign On dirichlet_boundary
    u_dirbdycells = u On (bf_cells On dirichlet_boundary)
    dir_fluxes = dir_sign * (u_dirbdycells - dirichlet_val)
    # Extending with zero away from Dirichlet boundaries (i.e. assuming no-flow elsewhere).
    return dir_fluxes On BoundaryFaces()
}

# Compute the residual for the heat equation.
compute_residual : Function(u : Collection Of Scalar On AllCells()) -> Collection Of Scalar On AllCells()
compute_residual(u) = {
    ifluxes = computeInteriorFlux(u)
    bfluxes = computeBoundaryFlux(u)
    # Extend both ifluxes and bfluxes to AllFaces() and add to get all fluxes.
    fluxes = (ifluxes On AllFaces()) + (bfluxes On AllFaces())
    # Deduced type: Collection Of Scalar On AllCells()
    residual = u - u0 + (dt / vol) * Divergence(fluxes)
    return residual
}

# NewtonSolve takes a function (that should accept the primary variable as input) and the initial guess for the primary variable.
# The primary variable decides which objects need to be AD objects in
# the back-end, the rule is the following:
#  1. The primary variable must be AD.
#  2. All objects that depend on an AD variable must be AD.
# In this concrete case, { u, fluxes, residual } must be AD.
explicitu = u0 + compute_residual(u0)
u = NewtonSolve(compute_residual, u0)

Output(u)
Output(fluxes)
































